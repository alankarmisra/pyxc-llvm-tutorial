(* pyxc.ebnf
   Chapter 8 grammar: semicolon-terminated blocks. *)

program        = { top } ;

top            = definition
                 | external
                 | expression ;

definition     = "def" prototype ":" block ;
external       = "extern" "def" prototype ;
toplevelexpr   = expression ;
block          = statement { ";" statement } [ ";" ] ;
statement      = "return" expression
               | expression ;

prototype      = identifier "(" [ identifier { "," identifier } ] ")" ;

expression     = comparison ;
comparison     = additive { compareop additive } ;
additive       = multiplicative { ("+" | "-") multiplicative } ;
multiplicative = primary { ("*" | "/" | "%") primary } ;

primary        = identifierexpr
                 | numberexpr
                 | parenexpr ;

identifierexpr = identifier
                 | identifier "(" [ expression { "," expression } ] ")" ;

numberexpr     = number ;
parenexpr      = "(" expression ")" ;

compareop      = "<" | ">" | "<=" | ">=" | "==" | "!=" ;

identifier     = letter { letter | digit | "_" } ;
number         = digit { digit } [ "." { digit } ]
                 | "." digit { digit } ;

letter         = "A".."Z" | "a".."z" ;
digit          = "0".."9" ;
