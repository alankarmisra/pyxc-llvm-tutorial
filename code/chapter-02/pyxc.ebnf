(* pyxc.ebnf
   Chapter 2: parser + AST *)

program        = { top } ;

top            = definition
                 | external
                 | expression ;

definition     = "def" prototype ":" "return" expression ;
external       = "extern" "def" prototype ;
toplevelexpr   = expression ;

prototype      = identifier "(" [ identifier { "," identifier } ] ")" ;

expression     = primary { binaryop primary } ;

primary        = identifierexpr
                 | numberexpr
                 | parenexpr ;

identifierexpr = identifier
                 | identifier "(" [ expression { "," expression } ] ")" ;

numberexpr     = number ;
parenexpr      = "(" expression ")" ;

binaryop       = "<" | ">" | "+" | "-" | "*" | "/" | "%" ;

identifier     = letter { letter | digit | "_" } ;
number         = digit { digit } [ "." { digit } ]
                 | "." digit { digit } ;

letter         = "A".."Z" | "a".."z" ;
digit          = "0".."9" ;
