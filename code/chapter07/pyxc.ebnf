(* pyxc.ebnf
   Chapter 7: optimization + JIT execution.
   Language syntax is still the chapter 5 core grammar,
   now used in a line-oriented REPL that can JIT-evaluate
   top-level expressions. *)

repl-session    = { repl-line } ;
repl-line       = [ top ] newline ;

program         = { top } ;

top             = definition
                | external
                | toplevel-expression ;

definition      = "def" prototype ":" return-statement ;
external        = "extern" "def" prototype ;
toplevel-expression = expression ;

prototype       = identifier "(" [ identifier { "," identifier } ] ")" ;

return-statement = "return" expression ;

expression      = primary { binaryop primary } ;

primary         = identifierexpr
                | numberexpr
                | parenexpr ;

identifierexpr  = identifier
                | identifier "(" [ expression { "," expression } ] ")" ;

numberexpr      = number ;
parenexpr       = "(" expression ")" ;

binaryop        = "<" | ">" | "+" | "-" | "*" | "/" | "%" ;

identifier      = letter { letter | digit | "_" } ;
number          = digit { digit } [ "." { digit } ]
                | "." digit { digit } ;

letter          = "A".."Z" | "a".."z" ;
digit           = "0".."9" ;
newline         = "\n" ;
