(* pyxc.ebnf
   Chapter 12 grammar: indentation blocks with if/while expressions. *)

program        = { top } ;

top            = definition
                 | external
                 | expression ;

definition     = "def" prototype ":" newline block ;
external       = "extern" "def" prototype ;
toplevelexpr   = expression ;
block          = indent statement { newline statement } dedent ;
statement      = "return" expression
               | expression ;

prototype      = identifier "(" [ identifier { "," identifier } ] ")" ;

expression     = comparison ;
comparison     = additive { compareop additive } ;
additive       = multiplicative { ("+" | "-") multiplicative } ;
multiplicative = primary { ("*" | "/" | "%") primary } ;

primary        = identifierexpr
                 | numberexpr
                 | parenexpr
                 | ifexpr
                 | whileexpr ;

identifierexpr = identifier
                 | identifier "(" [ expression { "," expression } ] ")" ;

numberexpr     = number ;
parenexpr      = "(" expression ")" ;
ifexpr         = "if" expression ":" newline suite
                 { "elif" expression ":" newline suite }
                 "else" ":" newline suite ;
whileexpr      = "while" expression ":" newline suite ;
suite          = indent expression { newline expression } dedent ;

compareop      = "<" | ">" | "<=" | ">=" | "==" | "!=" ;

identifier     = letter { letter | digit | "_" } ;
number         = digit { digit } [ "." { digit } ]
                 | "." digit { digit } ;

letter         = "A".."Z" | "a".."z" ;
digit          = "0".."9" ;

newline        = "NEWLINE" ;
indent         = "INDENT" ;
dedent         = "DEDENT" ;
